plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'org.neo4j'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

project.ext {
    arrowFlightGrpc = 'org.apache.arrow:flight-grpc:5.0.0'
    neo4j = [
        driverVersion: '4.3.3',
        serverVersion: '4.3.2',
    ]
    jupiterVersion = '5.7.0'
    sl4jVersion = '1.7.31'
}

dependencies {
    implementation (
            [group: 'org.neo4j.driver', name: 'neo4j-java-driver', version: project.ext.neo4j.driverVersion],
            [group: 'org.neo4j', name: 'neo4j-parser', version: project.ext.neo4j.serverVersion],
            project.ext.arrowFlightGrpc
    )
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.ext.jupiterVersion
    testRuntimeOnly (
            [group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.ext.jupiterVersion],
            [group: 'org.slf4j', name: 'slf4j-simple', version: project.ext.sl4jVersion],
    )
}

test {
    useJUnitPlatform()
}

project(':client') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'application'
    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'org.neo4j.arrow'
    version = parent.version

    application {
        mainClass.set("org.neo4j.arrow.Neo4jArrowClient")
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation rootProject
        implementation 'org.apache.arrow:flight-grpc:5.0.0'
        runtimeOnly 'org.slf4j:slf4j-simple:1.7.31'
    }

    jar {
        manifest {
            attributes 'Description': 'An Apache Arrow client for experimenting with neo4j-arrow'
        }
    }
}

project(':server') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'application'

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'org.neo4j.arrow'
    version = parent.version

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation rootProject
        implementation 'org.apache.arrow:flight-grpc:5.0.0'
        implementation 'org.neo4j.driver:neo4j-java-driver:4.3.3'
        runtimeOnly 'org.slf4j:slf4j-simple:1.7.31'
    }

    application {
        mainClass.set("org.neo4j.arrow.Neo4jArrowServer")
    }

    jar {
        manifest {
            attributes 'Description': 'An Apache Arrow server for fun and profit(?)'
        }
    }
}


project(':plugin') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'org.neo4j.arrow'
    version = parent.version

    repositories {
        maven {
            url "https://neo4j.bintray.com/enterprise-sources"
            credentials {
                // define user and password in your $HOME/.gradle/gradle.properties
                username neo4j_enterprise_source_user
                password neo4j_enterprise_source_password
            }
            authentication {
                basic(BasicAuthentication)
            }
            content {
                includeGroup 'com.neo4j'
            }
        }
        mavenCentral()
    }

    dependencies {
        implementation rootProject
        compileOnly rootProject.ext.arrowFlightGrpc
        compileOnly 'org.neo4j:neo4j-configuration:4.3.2'
        compileOnly 'org.neo4j:neo4j-kernel:4.3.2'
    }

    jar {
        manifest {
            attributes 'Description': 'TBD'
        }
    }

    shadowJar {
        dependencies {
            // Neo4j adds this for us
            exclude(dependency('org.neo4j.driver:neo4j-java-driver'))
            //exclude(dependency('org.slf4j:slf4j-simple'))
        }
        // minimize()
    }
}
