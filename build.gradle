plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'org.neo4j'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

project.ext {
    arrowFlightGrpc = 'org.apache.arrow:flight-grpc:5.0.0'
    neo4j = [
        driverVersion: '4.3.3',
        serverVersion: '4.3.2',
    ]
    jupiterVersion = '5.7.0'
    sl4jVersion = '1.7.31'
}

dependencies {
    implementation (
            [group: 'org.neo4j', name: 'neo4j-parser', version: project.ext.neo4j.serverVersion],
            [group: 'org.slf4j', name: 'slf4j-api', version: project.ext.sl4jVersion],
            project.ext.arrowFlightGrpc
    )
    testImplementation (
            [group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.ext.jupiterVersion],
            project(':client')
    )
    testRuntimeOnly (
            [group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.ext.jupiterVersion],
            [group: 'org.slf4j', name: 'slf4j-simple', version: project.ext.sl4jVersion],
    )
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching "org.neo4j.arrow.*"
    }
}

project(':client') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'application'
    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'org.neo4j.arrow'
    version = parent.version

    application {
        mainClass.set("org.neo4j.arrow.Neo4jArrowClient")
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation (
                rootProject,
                rootProject.ext.arrowFlightGrpc,
        )
        runtimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: rootProject.ext.sl4jVersion
    }

    jar {
        manifest {
            attributes 'Description': 'An Apache Arrow client for experimenting with neo4j-arrow'
        }
    }
}

project(':server') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'application'

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'org.neo4j.arrow'
    version = parent.version

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation (
                rootProject,
                rootProject.ext.arrowFlightGrpc,
                [group: 'org.neo4j.driver', name: 'neo4j-java-driver', version: rootProject.ext.neo4j.driverVersion],
        )
        runtimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: rootProject.ext.sl4jVersion
        testImplementation (
                [group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: rootProject.ext.jupiterVersion],
                // project(':client')
        )
        testRuntimeOnly (
                [group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: rootProject.ext.jupiterVersion],
                [group: 'org.slf4j', name: 'slf4j-simple', version: rootProject.ext.sl4jVersion],
        )
    }

    application {
        mainClass.set("org.neo4j.arrow.Neo4jArrowServer")
    }

    jar {
        manifest {
            attributes 'Description': 'An Apache Arrow server for fun and profit(?)'
        }
    }
}

project(':strawman') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'application'

    sourceCompatibility = 11
    targetCompatibility = 11

    group 'org.neo4j.arrow'
    version = parent.version

    application {
        mainClass.set("org.neo4j.arrow.Neo4jDirectClient")
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation (
                rootProject,
                [group: 'org.neo4j.driver', name: 'neo4j-java-driver', version: rootProject.ext.neo4j.driverVersion],
                [group: 'org.slf4j', name: 'slf4j-simple', version: rootProject.ext.sl4jVersion]
        )
    }

    jar {
        manifest {
            attributes 'Description': 'Basic Java Driver implementation'
        }
    }
}

project(':plugin') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'org.neo4j.arrow'
    version = parent.version

    test {
        useJUnitPlatform()
        filter {
            includeTestsMatching "org.neo4j.arrow.*"
        }
    }

    repositories {
        maven {
            url "https://neo4j.bintray.com/enterprise-sources"
            credentials {
                // define user and password in your $HOME/.gradle/gradle.properties
                username neo4j_enterprise_source_user
                password neo4j_enterprise_source_password
            }
            authentication {
                basic(BasicAuthentication)
            }
            content {
                includeGroup 'com.neo4j'
            }
        }
        mavenCentral()
    }

    dependencies {
        implementation rootProject
        implementation group: 'org.slf4j', name: 'slf4j-api', version: rootProject.ext.sl4jVersion

        compileOnly 'org.neo4j.gds:proc-catalog:1.6.4'
        compileOnly rootProject.ext.arrowFlightGrpc
        compileOnly 'org.neo4j:neo4j-configuration:4.3.2'
        compileOnly 'org.neo4j:neo4j-kernel:4.3.2'
        testImplementation (
                [group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: rootProject.ext.jupiterVersion],
                'org.neo4j:neo4j-kernel:4.3.2',
                project(':client')
        )
        testRuntimeOnly (
                [group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: rootProject.ext.jupiterVersion],
                [group: 'org.slf4j', name: 'slf4j-simple', version: rootProject.ext.sl4jVersion],
        )
    }


    jar {
        manifest {
            attributes 'Description': 'TBD'
        }
    }

    shadowJar {
        dependencies {
            // Neo4j adds this for us
            exclude(dependency('org.neo4j.driver:neo4j-java-driver'))
            //exclude(dependency('org.slf4j:slf4j-simple'))
        }
        // minimize()
    }
}
